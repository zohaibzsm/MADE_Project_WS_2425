
pipeline WDRPipeline {

    // World Development Report --> WDR

    // WDRPipeline pipeline 1
    WDRExtractor->WDRXLSXInterpreter->WDRSheetPicker->WDRFigureDataSelector;

    // WDRPipeline pipeline 2
    WDRFigureDataSelector->WDRColumnDeleter->WDRNameHeaders
    ->WDRTableInterpreter->WDRLoader;
    
    // WDRPipeline pipeline 3
    WDRFigureDataSelector->WDRColumnDeleter1->WDRNameHeaders1
    ->WDRTableInterpreter1->WDRLoader1;


// **************************** Pipeline #1 Blocks ********************************

    block WDRExtractor oftype HttpExtractor { 
            url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }
    
    block WDRXLSXInterpreter oftype XLSXInterpreter { }
    
    block WDRSheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    block WDRFigureDataSelector oftype CellRangeSelector {
        select: range P2:S45;
    }


// **************************** Pipeline #2 Blocks ********************************

    block WDRColumnDeleter oftype ColumnDeleter {
        delete: [column B, column C];
    }

    block WDRNameHeaders oftype CellWriter {
        at: range A1:B1;
        write: ["Country Code", "Bond Issuance Share"];
    }

    block WDRTableInterpreter oftype TableInterpreter { 
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype Bond_Issuance_Share_type
        ];
    }
    
    // Bond_Issuance_Share Constraint
    valuetype Bond_Issuance_Share_type oftype decimal {
        constraints: [ Bond_Issuance_Share_Const ];
    }
    constraint Bond_Issuance_Share_Const oftype RangeConstraint {
        lowerBound: 0;
        upperBound: 1;
    }
    // constraint Bond_Issuance_Share_Const on decimal :
    //     value >= 0 and value <= 1;

    block WDRLoader oftype SQLiteLoader {
        table: "bondIssuance";
        file: "./country-stats.sqlite";
    }


// **************************** Pipeline #3 Blocks ********************************

    block WDRColumnDeleter1 oftype ColumnDeleter {
        delete: [column B, column D];
    }

    block WDRNameHeaders1 oftype CellWriter {
        at: range A1:B1;
        write: ["Country Code", "GDP per Capita"];
    }

    block WDRTableInterpreter1 oftype TableInterpreter { 
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype GDP_per_Capita_type
        ];
    }

    // GDP_per_Capita Constraint
    valuetype GDP_per_Capita_type oftype decimal {
        constraints: [ GDP_per_Capita_Const ];
    }
    constraint GDP_per_Capita_Const on decimal :
        value > 0;

    block WDRLoader1 oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "./country-stats.sqlite";
    }
    
}